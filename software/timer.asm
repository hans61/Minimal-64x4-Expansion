; check vsync
; 60.096154 Hz -> vsync 16.6399999574 ms = 16.64 ms * 60 = 0.9984 seconds error 0.0016 s -> 1 second error after 625 s = 10:25 min
; 1st test stopped 24:57.63 for 25 min -> +2.37 seconds
; 2nd test stopped 01:02:54.09 for 01:03:00 -> +5.91 seconds
#org 0x2000
    LDI 0xfe STB 0xffff ; SP initialize
main:
    JPS _Clear CLZ _XPos CLZ _YPos
    JPS _Print "Timer: " , 0
    ; CLQ Z2
    CLZ sek
    CLZ min
    CLZ hour
    MIZ 60,millis
loop:
    JPS waitVsync
    DEZ millis
    BNE loop
    MIZ 60,millis
    ; a second is over
    INZ sek
    CIZ 60,sek
    BMI next    ; second ok (<60)
    CLZ sek
    INZ min
    CIZ 60,min
    BMI next    ; minutes ok (<60)
    CLZ min
    INZ hour
    CIZ 24,hour
    BMI next    ; Hours ok (<24)
    CLZ hour
next:
    MIZ 0,_YPos MIZ 7,_XPos
    ; Zeit ausgeben
    LDZ hour
    JAS PrDec
    LDI ':'
    JAS _PrintChar
    LDZ min
    JAS PrDec
    LDI ':'
    JAS _PrintChar
    LDZ sek
    JAS PrDec
    JPA loop
    
waitVsync:
    LDB vsync
    ANI 0x40
    CPI 0x00
    BEQ waitVsync   ; wait until high
vsync1:
    LDB vsync
    ANI 0x40
    CPI 0x00
    BNE vsync1
    RTS
; ################################## Subroutines ###################################
; ----------------------------------------------
; delay A * 1 ms
delay_ms:
    PHS         ; 8
    MIZ 194,Z0  ; 4 ( + 8 ) = 12 * 0,125 = 1,5 µs 
delay1:         ; n * 0,125 * (32+5+4) = n * 5,125 µs
    NOP NOP     ; 32
    DEZ Z0      ; 5
    BNE delay1  ; 4/3 -> 194 * 5,125 = 994,25 - 0,125 = 994,125 µs
    PLS         ; 6
    DEC         ; 3
    BNE delay_ms    ; 4/3
    RTS         ; 10 ( + 3 + 3 + 6 ) = 22 * 0,125 = 2,75 + 1,5 µs = 4,25 µs + 994,125 µs + 2 µs (call) = 1000,375s
; ----------------------------------------------
PrDec:
    STZ tmp
    MIZ 0xff,zif
PrDec100:
    INZ zif
    SIZ 100,tmp
    BCS PrDec100
    AIZ 100,tmp
    ;LDI 0x30
    ;ADZ zif
    ;JAS _PrintChar
    MIZ 0xff,zif
PrDec10:
    INZ zif
    SIZ 10,tmp
    BCS PrDec10
    AIZ 10,tmp
    LDI 0x30
    ADZ zif
    JAS _PrintChar
    LDZ tmp
    ADI 0x30
    JAS _PrintChar
    RTS
; ----------------------------------------------
value:  0x00
mask:
    0x01,   ; right
    0x02,   ; left
    0x04,   ; up
    0x08,   ; down
    0x10,   ; fire
    0x20,   ;
    0x40,   ;
    0x80    ;

#mute
#org 0x0000
tmp:    0x00,
zif:    0x00,
millis: 0x00,
sek:    0x00,
min:    0x00,
hour:   0x00,
Z0:     0x00,
Z1:     0x00,
Z2:     0x00000000,
PtrD:   0x0000,


#org 0xfee0 outPort1:   ; SN76489 data port (4HC574)
#org 0xfee1 vsync:      ; 4HC574 input Kempston vsync
#org 0xfee2 outPort2:   ; 74HC74 port bit0 0x01=CS SD card bit1 0x02 WR SN76489


; MinOS API definitions generated by 'asm os.asm -s_'
#org 0xf000 _Start:
#org 0xf003 _Prompt:
#org 0xf006 _MemMove:
#org 0xf009 _Random:
#org 0xf00c _ScanPS2:
#org 0xf00f _ResetPS2:
#org 0xf012 _ReadInput:
#org 0xf015 _WaitInput:
#org 0xf018 _ReadLine:
#org 0xf01b _SkipSpace:
#org 0xf01e _ReadHex:
#org 0xf021 _SerialWait:
#org 0xf024 _SerialPrint:
#org 0xf027 _FindFile:
#org 0xf02a _LoadFile:
#org 0xf02d _SaveFile:
#org 0xf030 _ClearVRAM:
#org 0xf033 _Clear:
#org 0xf036 _ClearRow:
#org 0xf039 _ScrollUp:
#org 0xf03c _ScrollDn:
#org 0xf03f _Char:
#org 0xf042 _PrintChar:
#org 0xf045 _Print:
#org 0xf048 _PrintPtr:
#org 0xf04b _PrintHex:
#org 0xf04e _SetPixel:
#org 0xf051 _Line:
#org 0xf054 _Rect:
#org 0x00c0 _XPos:
#org 0x00c1 _YPos:
#org 0x00c2 _RandomState:
#org 0x00c6 _ReadNum:
#org 0x00c9 _ReadPtr:
#org 0x00cd _ReadBuffer: